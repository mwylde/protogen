hci_message = {
  @message_type: u8;
  public message: choose {
    HciCommand = hci_command(@message_type) |
    HciEvent = hci_event(@message_type)
  };
}

////////////////////////////
///       HCI EVENTS     ///
////////////////////////////
hci_event ($type: u8 = 0x04) = {
  @event_code: u8;
  @parameter_length: u8;
  @data: [u8; @parameter_length];
  public event: apply @data choose {
    CommandComplete = command_complete(@event_code)
  };
}

command_complete ($event_code: u8 = 0x0E) = {
  // number of hci packets which are allowed to be sent to the controller
  public @num_packets: u8;
  @opcode: u16;
  ogf: u8 = @opcode >> 10;
  @ocf: u16 = @opcode & 0x400 - 1;
  public @response: choose {
    NoAssociatedCommand = no_associated_command(@ocf) |
    Reset = reset_response(@ocf) |
    SetEventFilter = set_event_filter_response(@ocf) |
    Flush = flush_response(@ocf) |
    WriteLocalName = write_local_name_response(@ocf) |
    ReadLocalName = read_local_name_response(@ocf) |
    ReadConnectionAcceptTimeout = read_connection_accept_timeout_response(@ocf) |
    WriteConnectionAcceptTimeout = write_connection_accept_timeout_response(@ocf) |
    ReadPageTimeout = read_page_timeout_response(@ocf) |
    WritePageTimeout = write_page_timeout_response(@ocf) |
    ReadScanEnable = read_scan_enable_response(@ocf) |
    WriteScanEnable = write_scan_enable_response(@ocf) |
    ReadPageScanActivity = read_page_scan_activity_response(@ocf) |
    WritePageScanActivity = write_page_scan_activity_response(@ocf) |
    ReadInquiryScanActivity = read_inquiry_scan_activity_response(@ocf) |
    WriteInquiryScanActivity = write_inquiry_scan_activity_response(@ocf)
  };
}

// sent to update the number of command packets that can be sent
no_associated_command ($ocf: u16 = 0x0000) = {}

response_status = {
  public status_code: u8;
}

////////////////////////////
///     HCI COMMANDS     ///
////////////////////////////

hci_command ($type: u8 = 0x01) = {
  @opcode: u16;
  ogf: u8 = @opcode >> 10;
  @ocf: u16 = @opcode & 0x400 - 1;
  @length: u8;
  @data: [u8; @length];
  public command: apply @data choose {
    Reset = reset(@ocf) |
    SetEventFilter = set_event_filter(@ocf) |
    Flush = flush(@ocf) |
    WriteLocalName = write_local_name(@ocf) |
    ReadLocalName = read_local_name(@ocf) |
    ReadConnectionAcceptTimeout = read_connection_accept_timeout(@ocf) |
    WriteConnectionAcceptTimeout = write_connection_accept_timeout(@ocf) |
    ReadPageTimeout = read_page_timeout(@ocf) |
    WritePageTimeout = write_page_timeout(@ocf) |
    ReadScanEnable = read_scan_enable(@ocf) |
    WriteScanEnable = write_scan_enable(@ocf) |
    ReadPageScanActivity = read_page_scan_activity(@ocf) |
    WritePageScanActivity = write_page_scan_activity(@ocf) |
    ReadInquiryScanActivity = read_inquiry_scan_activity(@ocf) |
    WriteInquiryScanActivity = write_inquiry_scan_activity(@ocf)
  };
}

// reset
reset ($ocf: u16 = 0x0003) = {}
reset_response ($ocf: u16 = 0x0003) = {
  public status: response_status();
}

// set_event_filter
set_event_filter ($ocf: u16 = 0x0005) = {
  @filter_type: u8;
  public filter: choose {
    ClearAllFilter = clear_all_filter(@filter_type) |
    InquiryResult = inquiry_result(@filter_type) |
    ConnectionSetup = connection_setup(@filter_type)
  };
}
set_event_filter_response ($ocf: u16 = 0x0005) = {
  public status: response_status();
}

inquiry_result ($filter_type: u8 = 0x01) = {
  public condition: filter_condition();
}

connection_setup ($filter_type: u8 = 0x02) = {
  public condition: filter_condition();
  public auto_accept: u8;
}

filter_condition = {
  @condition_type: u8;
  public value: choose {
    AllDevices = all_devices(@condition_type) |
    MatchClass = match_class(@condition_type) |
    MatchAddress = match_address(@condition_type)
  };
}

clear_all_filter($filter_type: u8 = 0x00) = {}

all_devices ($condition_type: u8 = 0x00) = {}

match_class ($condition_type: u8 = 0x01) = {
  public class_of_device: [u8; 3];
  public class_of_device_mask: [u8; 3];
}

match_address ($condition_type: u8 = 0x02) = {
  public address: [u8; 6];
}

// flush
flush ($ocf: u16 = 0x0008) = {
  public connection_handle: u16;
}

flush_response ($ocf: u16 = 0x0008) = {
  public status: response_status();
  public connection_handle: u16;
}

// TODO: read_pin_type_command
// TODO: write_pin_type_command
// TODO: create_new_unit_key
// TODO: read_stored_link_key
// TODO: write_stored_link_key
// TODO: delete_stored_link_key

write_local_name ($ocf: u16 = 0x0013) = {
  @local_name_buffer: [u8; 248];
  public local_name: apply @local_name_buffer cstring;
}

write_local_name_response ($ocf: u16 = 0x0013) = {
  public status: response_status();
}

read_local_name ($ocf: u16 = 0x0014) = {}

read_local_name_response ($ocf: u16 = 0x0014) = {
  public status: response_status();
  @local_name_buffer: [u8; 248];
  public local_name: apply @local_name_buffer cstring;
}

// connection accept timeout
read_connection_accept_timeout ($ocf: u16 = 0x0015) = {
}

read_connection_accept_timeout_response ($ocf: u16 = 0x0015) = {
  public status: response_status();
  public connection_accept_timeout: u16;
}

write_connection_accept_timeout ($ocf: u16 = 0x0016) = {
  public connection_accept_timeout: u16;
}

write_connection_accept_timeout_response ($ocf: u16 = 0x0016) = {
  public status: response_status();
}

// page timeout
read_page_timeout ($ocf: u16 = 0x0017) = {
}

read_page_timeout_response ($ocf: u16 = 0x0017) = {
  public status: response_status();
  public page_timeout: u16;
}

write_page_timeout ($ocf: u16 = 0x0018) = {
  public page_timeout: u16;
}

write_page_timeout_response ($ocf: u16 = 0x0018) = {
  public status: response_status();
}


// scan enable
read_scan_enable ($ocf: u16 = 0x0019) = {
}

read_scan_enable_response ($ocf: u16 = 0x0019) = {
  public status: response_status();
  public scan_enable: u8;
}

write_scan_enable ($ocf: u16 = 0x001A) = {
  public scan_enable: u8;
}

write_scan_enable_response ($ocf: u16 = 0x001A) = {
  public status: response_status();
}


// page scan activity
read_page_scan_activity ($ocf: u16 = 0x001B) = {
}

read_page_scan_activity_response ($ocf: u16 = 0x001B) = {
  public status: response_status();
  public page_scan_interval: u16;
  public page_scan_window: u16;
}

write_page_scan_activity ($ocf: u16 = 0x001C) = {
  public page_scan_interval: u16;
  public page_scan_window: u16;
}

write_page_scan_activity_response ($ocf: u16 = 0x001C) = {
  public status: response_status();
}

// inquiry scan activity
read_inquiry_scan_activity ($ocf: u16 = 0x001D) = {
}

read_inquiry_scan_activity_response ($ocf: u16 = 0x001D) = {
  public status: response_status();
  public inquiry_scan_interval: u16;
  public inquiry_scan_window: u16;
}

write_inquiry_scan_activity ($ocf: u16 = 0x001E) = {
  public inquiry_scan_interval: u16;
  public inquiry_scan_window: u16;
}

write_inquiry_scan_activity_response ($ocf: u16 = 0x001E) = {
  public status: response_status();
}
